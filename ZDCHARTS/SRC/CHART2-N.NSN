* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** CHART2-N.NSN
/** Version: 0.8
/** Copyright 2015 Thomas Schwade
/** http://www.zerodrive.net
/** Licensed under the EUPL V.1.1
/** https://github.com/zerodrive/NJX-ZDCharts/blob/master/LICENSE.pdf
/** Serialize the data structure CHART2-A.
/** Usage: CALLNAT "CHART2-N" CHART2-A STRDATA (RES)
/**
DEFINE DATA
PARAMETER USING CHART2-A
PARAMETER
1 STRDATA (U) DYNAMIC
1 RES (I4) OPTIONAL
LOCAL
1 #I (I2)
1 #J (I2)
1 #NUM (P9.2)
1 #STR (U13)
END-DEFINE
*
OPTIONS TQMARK = OFF
RESET STRDATA
IF RES SPECIFIED
  RESET RES
END-IF
*
COMPRESS STRDATA '[' TO STRDATA LEAVING NO
FOR #I := 1 TO *OCC(CHART2-A.PNAME,1)
  COMPRESS STRDATA '{' TO STRDATA LEAVING NO
  #NUM := CHART2-A.DDATA(#I)
  PERFORM FORMATNUMERIC
  COMPRESS STRDATA '"value":' #STR TO STRDATA LEAVING NO
  IF *OCC(CHART2-A.PNAME,2) GT 0
    COMPRESS STRDATA ',' TO STRDATA LEAVING NO
  END-IF
  FOR #J := 1 TO *OCC(CHART2-A.PNAME,2)
    COMPRESS STRDATA '"' CHART2-A.PNAME(#I,#J) '":"'
      CHART2-A.PVALUE(#I,#J) '"' TO STRDATA LEAVING NO
    IF #J LT *OCC(CHART2-A.PNAME,2)
      COMPRESS STRDATA ',' TO STRDATA LEAVING NO
    END-IF
  END-FOR
  COMPRESS STRDATA '}' TO STRDATA LEAVING NO
  IF #I LT *OCC(CHART2-A.PNAME,1)
    COMPRESS STRDATA ',' TO STRDATA LEAVING NO
  END-IF
END-FOR
COMPRESS STRDATA ']' TO STRDATA LEAVING NO
*
DEFINE SUBROUTINE FORMATNUMERIC
IF #NUM / 100 * 100 = #NUM
  MOVE EDITED #NUM (EMU=Z(8)9) TO #STR
ELSE
  MOVE EDITED #NUM (EMU=Z(8)9.99) TO #STR
END-IF
END-SUBROUTINE
*
END
